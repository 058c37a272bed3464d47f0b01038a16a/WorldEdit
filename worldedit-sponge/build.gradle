apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenCentral()
    maven {
        name 'Sponge'
        url 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile project(':worldedit-core')
    compile "org.spongepowered:spongeapi:2.1-SNAPSHOT"
}

shadowJar {
    dependencies {
        include(dependency(':worldedit-core'))
    }
}


task processSources(type: Copy) {
    from (sourceSets.main.java) {
        include "**/SpongeWorldEdit.java"
        filter { line -> line.replaceAll("%VERSION%", project.version) }
    }
    from (sourceSets.main.java) {
        exclude "**/SpongeWorldEdit.java"
    }
    into 'build/sources/main/java'
}

compileJava {
    source = file('build/sources/main/java')
    dependsOn processSources
}

build.dependsOn(shadowJar)